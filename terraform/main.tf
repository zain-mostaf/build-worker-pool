##
# Copyright (C) IBM Inc. - All Rights Reserved
# 
# This source code is protected under international copyright law.  All rights
# reserved and protected by the copyright holders.
# This file is confidential and only available to authorized individuals with the
# permission of the copyright holders.  If you encounter this file and do not have
# permission, please contact the copyright holders and delete this file.
# 
# This software is provided as-is, without warranties of any kind. 
##

locals {
  workspace_id = var.schematics_workspace_id
}

data "ibm_schematics_workspace" "schematics_workspace" {
  
  workspace_id = local.workspace_id
}

/*
data "ibm_schematics_output" "output" {
  workspace_id = local.workspace_id
  template_id  = data.ibm_schematics_workspace.schematics_workspace.template_id.0
}
*/

data shell_script workspace_output_variables {
    lifecycle_commands {
        read = "/bin/bash getTFOutputs.sh ${local.workspace_id}"
    }
}

// validation of available slots for networking
data shell_script validate_pool_cidr_size {
    lifecycle_commands {
        read = "/usr/bin/python3 validatePoolCIDRSize.py ${length(local.worker_pool_ips)} ${local.worker_pool_size}"
    }
}

// validation for ad_join_password field against password protection coming from parent workspace
data shell_script asterisk_received_in_password {
    lifecycle_commands {
        read = <<EOF
        AP=`echo '${local.ad_password}' | grep '\*\*\*'`
        if [ "$AP" != "" ]
        then
             echo "Please check your AD Password. It seems to be encrypted and invalid."
             exit 1
        fi
        echo '{ "message" : "ok" }'
        EOF
    }
}

locals {

    // New HPC Management offerings outputs all needed data to be consumed by this worker pool.
    // For the old HPC management offerings, the worker pool will look at input variables.
    output = data.shell_script.workspace_output_variables.output

    /**
    * Declared input variables in the HPC Management Schematics workspace
    */
    zone = try(try(local.output.zone, [for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "zone" ][0]), "")

    // Region: calculated based on zone (needed for ibm builder provider)
    region = "${split("-", local.zone)[0]}-${split("-", local.zone)[1]}"

    cluster_prefix = try(try(local.output.cluster_prefix, [for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "cluster_prefix" ][0]), "")
    tags = try(try(jsondecode(local.output.tags), jsondecode([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "tags" ][0])), [])
    ssh_keys = try(try(local.output.ssh_keys, [for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "ssh_keys" ][0]), "")

    symphony_cluster_info = try(
    try ( local.output.symphony_cluster_info, 
        try(
              [for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "symphony_cluster_info" ][0],
              [for input in data.ibm_schematics_workspace.schematics_workspace.template_values_metadata : input.default if input.name == "symphony_cluster_info"][0]
            ),
        ),
    "")

    ad_dns_ips = try(try(local.output.ad_dns_ips, [for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "ad_dns_ips" ][0]), "")
    ad_domain = try(try(local.output.ad_domain, [for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "ad_domain" ][0]), "")
    ad_user = var.ad_join_user != "" ? var.ad_join_user : try(
    try (local.output.ad_user,
        try(
             [for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "ad_user"][0],
             [for input in data.ibm_schematics_workspace.schematics_workspace.template_values_metadata : input.default if input.name == "ad_user"][0] 
           ),
        ),
    "")
        
    // FIXME: Is ad_password sensitive?    
    ad_password = var.ad_join_password != "" ? var.ad_join_password : try(try(local.output.ad_password, [for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "ad_password" ][0]), "")
    
    /*
    * Declared input variables in the HPC Managemeny Schematics workspace that can be overwritten by this workspace
    */
     symphony_compute_instance_profile = try([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "symphony_compute_instance_profile" ][0], "")
     symphony_linux_image_name = try(try(local.output.symphony_image_name, [for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "symphony_image_name" ][0]), "")
     symphony_windows_image_name = try([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "windows_image_name" ][0], "")

    // Computed grid-manager-prefix name (used to configure ego.conf on workers) - according to Citi conventions
    symphony_master_names = [for i in range(2) : "${local.cluster_prefix}-${local.ad_domain != "" ? "gm" : "grid-man" }-${format("%02d", i+1)}"]

    /*
    *  Output variables coming from HPC Management Schematics workspace (requires commit 7f45c3fa7c0d85e3bb02ccb2afeb8b5fd178046b in citi-hpc-offering to work)
    */
    workload_vpc_name = try(
        local.output.vpc_name,
        try([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "vpc_name"][0], "")
    )
    resource_group_name = try(
        local.output.resource_group_name,
        try([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "resource_group_name"][0], "")
    )
    workload_vpc_id = try(local.output.workload_vpc_id, try(data.ibm_is_vpc.workload_vpc[0].id, ""))
    resource_group_id = try(local.output.resource_group_id, try(data.ibm_resource_group.worker_resource_group[0].id, ""))
    private_dns_instance_id = try(
        local.output.private_dns_instance_id,
        try([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "private_dns_instance_id"][0], "")
    )
    private_dns_zone_id = try(
        local.output.private_dns_zone_id,
        try([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "private_dns_zone_id"][0], "")
    )
    private_dns_reverse_zone_id = try(
        local.output.private_dns_reverse_zone_id,
        try([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "private_dns_reverse_zone_id"][0], "")
    )
    ssh_key_ids = try(jsondecode(local.output.ssh_key_ids), [])

    vni_enabled = try(tobool(local.output.vni_enabled), try(
        tobool([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "vni_enabled"][0]),
        true
    ))
    vni_name = try(
        local.output.vni_name,
        try([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "vni_name"][0], "eth1")
    )
    vni_subnet_id_or_name = try(
        local.output.vni_subnet_id_or_name,
        try([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "vni_subnet_id_or_name"][0], "")
    )
    vni_security_group_ids = try(
        jsondecode(local.output.vni_security_group_ids),
        try(jsondecode([for input in data.ibm_schematics_workspace.schematics_workspace.template_inputs : input.value if input.name == "vni_security_group_ids"][0]), [])
    )

    /*
    *  Output that can be overwritten by this workspace
    */
    symphony_subnet_id = var.symphony_subnet_id != "" ? var.symphony_subnet_id : try(local.output.symphony_subnet_id, "")
    symphony_worker_security_group = length(var.security_groups) > 0 ? flatten([for sg_name in var.security_groups : [for sg in module.security_groups.security_groups : sg.id if sg.name == sg_name]]) : try(jsondecode(local.output.symphony_worker_security_group), [])

    symphony_instance_profile = var.symphony_instance_profile != "" ? var.symphony_instance_profile : local.symphony_compute_instance_profile
    symphony_instance_image_id = var.symphony_instance_image_id != "" ? var.symphony_instance_image_id : (local.symphony_windows_image_name != "" ? local.symphony_windows_image_name : local.symphony_linux_image_name)


    // Worker pool size, name and IPs
    worker_pool_subnet_segmentation = var.worker_pool_subnet_segmentation
    worker_pool_subnet_start_host = var.worker_pool_subnet_start_host
    worker_pool_size = var.worker_pool_size
    worker_pool_prefix = var.worker_pool_prefix
    worker_pool_start_number_at = var.worker_pool_start_at_number
    worker_pool_ips = flatten([for subnet in local.worker_pool_subnet_segmentation : [for index in range(local.worker_pool_subnet_start_host, pow(2, 32 - split("/", subnet)[1])) : cidrhost(subnet, index)]])
    worker_pool_name_prefix = local.worker_pool_prefix!="" ? "${local.worker_pool_prefix}" : ("${local.ad_domain != "" ? "wk" : "worker" }")
    worker_pool_worker_names = [for i in range(local.worker_pool_size) : "${local.cluster_prefix}-${local.worker_pool_name_prefix}-${format("%04d", i + local.worker_pool_start_number_at)}"]
    worker_pool_ip_name_mapping = {for idx in range(min(local.worker_pool_size, length(local.worker_pool_ips))) : local.worker_pool_ips[idx] => local.worker_pool_worker_names[idx]}

    // Worker pool type
    worker_pool_type = var.worker_pool_type
    // Worker pool OS
    worker_os = var.worker_pool_os

    // Cloud-INIT --> Post deployment tasks
    post_deployment_tasks = var.symphony_post_deployment_tasks

    // SSL Certificates
    ego_ssl_cacert = var.ego_ssl_cacert

    worker_attributes=try(var.worker_attributes, {})

    skip_symphony_config = var.skip_symphony_config
   
}

data "ibm_is_vpc" "workload_vpc" {
    count = local.workload_vpc_name != "" ? 1 : 0
    provider = ibm.builder
    name = local.workload_vpc_name
}

data "ibm_resource_group" "worker_resource_group" {
    count = local.resource_group_name != "" ? 1 : 0
    provider = ibm.builder
    name = local.resource_group_name
}

// Get all resource groups from the region
module security_groups {
    providers = {
        ibm = ibm.builder
    }

    source = "./modules/all-security-groups"
}

// Create DNS records
module dns_records {
    providers = {
        ibm = ibm.builder
    }
    depends_on=[data.shell_script.validate_pool_cidr_size]
    count = local.private_dns_instance_id != "" && local.private_dns_zone_id != "" ? 1 : 0
    source = "./modules/dns-entry"
    ibmcloud_api_key = var.ibmcloud_api_key
    private_dns_instance_id = local.private_dns_instance_id
    private_dns_zone_id = local.private_dns_zone_id
    private_dns_reverse_zone_id = local.private_dns_reverse_zone_id
    machine_ip_name_mapping = local.worker_pool_ip_name_mapping
}

// Create cloud-init scripts depending on operating system
module cloud_init_scripts {
    depends_on=[data.shell_script.validate_pool_cidr_size]
    source = "./modules/cloud-init-scripts"
    worker_os=local.worker_os
    cluster_domain=try(module.dns_records[0].domain_name, local.ad_domain)
    ego_cluster_info=local.symphony_cluster_info
    ego_config_override=""
    post_deployment_tasks=local.post_deployment_tasks
    additional_resource_tag=""
    ego_master_list=local.symphony_master_names
    ego_ssl_cacert=local.ego_ssl_cacert
    ad_info = {
        ad_dns_server = local.ad_dns_ips
        ad_domain = local.ad_domain
        ad_join_user = local.ad_user
        ad_join_password = local.ad_password
    }
    worker_attributes=local.worker_attributes
    skip_symphony_config = local.skip_symphony_config
}

// Create shared workers
module shared_workers {
    providers = {
        ibm = ibm.builder
    }
    depends_on=[data.shell_script.validate_pool_cidr_size]
    source = "./modules/shared-worker"
    count = local.worker_pool_type == "shared" ? 1 : 0
    machine_ip_name_mapping = local.worker_pool_ip_name_mapping
    symphony_image_name = local.symphony_instance_image_id
    symphony_profile = local.symphony_instance_profile
    worker_tags = local.tags
    security_group_ids = local.symphony_worker_security_group
    zone = local.zone
    subnet_id_or_name = local.symphony_subnet_id
    resource_group_id = local.resource_group_id
    ssh_keys = local.ssh_key_ids
    cloud_init_script = module.cloud_init_scripts.cloud_init_output
    vpc_id = local.workload_vpc_id
    vni_enabled = local.vni_enabled
    vni_name = local.vni_name
    vni_subnet_id_or_name = local.vni_subnet_id_or_name
    vni_security_group_ids = local.vni_security_group_ids
}
// Create dedicated workers

// Create bare metal workers